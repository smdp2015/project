grammar dk.itu.smdp2015.church.Configurator with org.eclipse.xtext.common.Terminals

import "http://itu.dk/smdp/configurator" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Configurator:
	'configurator' name=ID 
	(description=STRING)?
	'{' parameters+=AbstractParameter ( ','? parameters+=AbstractParameter)* '}';

AbstractParameter:
	ParameterGroup | Parameter;
	
ParameterGroup:
	'group'	name=ID
	(description=STRING)?
	( ('visible-if' visibility=Expression)?
	& ('constraints' '{' constraints+=Constraint ( ',' constraints+=Constraint)* ','? '}' )?
	& '{' parameters+=AbstractParameter ( ','? parameters+=AbstractParameter)* ','? '}' );

Parameter:
	'parameter'	name=ID
	(description=STRING)?
	( ((optional?='optional')|'mandatory')? 
	& ('visible-if' visibility=Expression)?
	& ('default-value' default=Expression)?
	& ('constraints' '{' constraints+=Constraint ( ',' constraints+=Constraint)* '}' )?
	& 'values' valueRange=ValueRange );

ValueRange:
	Enumerated | Bounded;

Enumerated returns Enumerated:
	'(' values+=Expression ( ',' values+=Expression)* ')';

Bounded returns Bounded:
	'[' lowerBound=Expression ';' upperBound=Expression ']';

Constraint:
	('description' description=STRING)?	
	 expression=Expression;

Expression:
	LogicalOr;

enum LogicalOrOperator returns BinaryOperator:
	logicalOr = 'or' ;

LogicalOr returns Expression:
	LogicalAnd ( {Binary.left=current} operator=LogicalOrOperator right=LogicalAnd )*;

enum LogicalAndOperator returns BinaryOperator:
	logicalAnd = 'and';

LogicalAnd returns Expression:
	Equality ( {Binary.left=current} operator=LogicalAndOperator right=Equality )*;

enum EqualityOperator returns BinaryOperator:
	equal = '==' | notEqual = '!=';

Equality returns Expression:
	Comparative ( {Binary.left=current} operator=EqualityOperator right=Comparative )*;

enum ComparativeOperator returns BinaryOperator:
	lessThan = '<' | greaterThan = '>' ;

Comparative returns Expression:
	Additive ( {Binary.left=current} operator=ComparativeOperator right=Additive )*;

enum AdditiveOperator returns BinaryOperator:
	addition = '+' | subtraction = '-';

Additive returns Expression:
	Multiplicative ( {Binary.left=current} operator=AdditiveOperator right=Multiplicative )*;

enum MultiplicativeOperator returns BinaryOperator:
	multiplication = '*';

Multiplicative returns Expression:
	Primitive ( {Binary.left=current} operator=MultiplicativeOperator right=Primitive )*;

Primitive returns Expression:	
	Unary | InRange | Integer | Boolean | String0 | Identifier | '(' Expression ')';

enum UnaryOperator:
	inversion = '-' | logicalNot = 'not';

Unary:
	operator=UnaryOperator inner=Primitive;

Constant:
	Integer | Boolean | String0;

InRange:
	parameter=[Parameter] 'in' range=ValueRange;

Integer:
	value=EInt;

Boolean:
	value=EBoolean;

String0 returns String:
	value=STRING;

Identifier:
	id=[Parameter];

EInt returns ecore::EInt:
	/* '-'? */ INT;

EDouble returns ecore::EDouble:
	/* '-'? */ INT? '.' INT (('E'|'e') '-'? INT)?;

EBoolean returns ecore::EBoolean:
	'true' | 'false';
